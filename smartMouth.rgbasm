include "utils.rgbinc"
include "alphabet.rgbinc"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

def TILES_COUNT                     equ (384)
def BYTES_PER_TILE                  equ (16)
def TILES_BYTE_SIZE                 equ (TILES_COUNT * BYTES_PER_TILE)
def TILEMAP_BYTE_SIZE               equ (1024)
def GRAPHICS_DATA_SIZE              equ (TILES_BYTE_SIZE + TILEMAP_BYTE_SIZE + TILEMAP_BYTE_SIZE)
def GRAPHICS_DATA_ADDRESS_END       equ ($8000)
def GRAPHICS_DATA_ADDRESS_START     equ (GRAPHICS_DATA_ADDRESS_END - GRAPHICS_DATA_SIZE)
def TILE_ALPHABET_SIZE              equ (16 * 16 * 26)
def TILE_ALPHABET_START             equ (GRAPHICS_DATA_ADDRESS_START - TILE_ALPHABET_SIZE)
def ALPHABET_MAP_SIZE               equ (128)
def ALPHABET_MAP_START              equ (TILE_ALPHABET_START - ALPHABET_MAP_SIZE)

def INITIAL_PLAYER_ARROW_ADDRESS    equ (_SCRN0 + 10 * SCRN_VX_B + 2)
def ARROW_ADDRESS_STEP              equ (SCRN_VX_B * 2)
def SCORE_ADDRESS_STEP              equ (ARROW_ADDRESS_STEP)
def PLAYER_1_SCORE_ADDRESS          equ (INITIAL_PLAYER_ARROW_ADDRESS + 12)
def PLAYER_2_SCORE_ADDRESS          equ (PLAYER_1_SCORE_ADDRESS + SCORE_ADDRESS_STEP)
def PLAYER_3_SCORE_ADDRESS          equ (PLAYER_2_SCORE_ADDRESS + SCORE_ADDRESS_STEP)
def PLAYER_4_SCORE_ADDRESS          equ (PLAYER_3_SCORE_ADDRESS + SCORE_ADDRESS_STEP)

def INITIAL_MENU_ARROW_ADDRESS      equ (_SCRN1 + 2)

def SAVE_HEADER_D                   equ $44
def SAVE_HEADER_M                   equ $4D
def SAVE_HEADER_G                   equ $47

def WINDOW_X                        equ 7
def WINDOW_Y                        equ 80
def WINDOW_HIDDEN_Y                 equ 145

def MODE_GAMEPLAY                   equ 0
def MODE_MENU                       equ 1
def MODE_EDIT_NAMES                 equ 2

def MENU_SAVE_GAME                  equ 0
def MENU_RESET_SCORES               equ 1
def MENU_EDIT_NAMES                 equ 2
def MENU_EXIT_MENU                  equ 3

def KEYBOARD_HIGHLIGHT_MIN_X        equ 16
def KEYBOARD_HIGHLIGHT_MIN_Y        equ 24
def KEYBOARD_HIGHLIGHT_MAX_X        equ 88
def KEYBOARD_HIGHLIGHT_MAX_Y        equ 48

def EDIT_PLAYER_NAME_ROW            equ 6
def EDIT_PLAYER_NAME_ROW_PX         equ (EDIT_PLAYER_NAME_ROW * 8 + 16)
def EDIT_PLAYER_NAME_DISPLAY        equ (_SCRN0 + EDIT_PLAYER_NAME_ROW * 32 + 1)

def EDIT_INSTRUCTIONS_SOURCE       equ (GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + TILEMAP_BYTE_SIZE + 27 * SCRN_VX_B)
def EDIT_INSTRUCTIONS_DESTINATION  equ (_SCRN0 + 32 * 14)

rsset _RAM

def WRAM_IS_VBLANK                  rb 1
def WRAM_PAD_INPUT                  rb sizeof_PAD_INPUT

def WRAM_MODE                       rb 2
def WRAM_TILE_0_X                   rb 1
def WRAM_TILE_0_Y                   rb 1
def WRAM_TILE_1_X                   rb 1
def WRAM_TILE_1_Y                   rb 1
def WRAM_RANDOM_SALT                rb 1
def WRAM_TILE_0_CHAR                rb 1
def WRAM_TILE_1_CHAR                rb 1
def WRAM_TILE_TO_UPDATE             rb 1
def WRAM_TILE_INDEX                 rb 1
def WRAM_PLAYER_ARROW_INDEX         rb 1
def WRAM_PLAYER_1_NAME              rb 8
def WRAM_PLAYER_2_NAME              rb 8
def WRAM_PLAYER_3_NAME              rb 8
def WRAM_PLAYER_4_NAME              rb 8
def WRAM_PLAYER_1_SCORE             rb 1
def WRAM_PLAYER_2_SCORE             rb 1
def WRAM_PLAYER_3_SCORE             rb 1
def WRAM_PLAYER_4_SCORE             rb 1
def WRAM_MENU_ARROW_INDEX           rb 1
def WRAM_GAME_INTERFACE_NEEDS_INIT  rb 1
def WRAM_EDIT_INTERFACE_NEEDS_INIT  rb 1
def WRAM_KEY_HIGHLIGHT_X            rb 1
def WRAM_KEY_HIGHLIGHT_Y            rb 1
def WRAM_EDIT_NAME_PLAYER_INDEX     rb 2
def WRAM_EDIT_NAME_CHAR_INDEX       rb 1

def WRAM_END                        rb 0

; sanity checks
def WRAM_USAGE                      equ (WRAM_END - _RAM)
println "WRAM usage: {d:WRAM_USAGE} bytes"
assert WRAM_USAGE <= $2000, "Too many bytes used in WRAM"

; load the graphics data from ROM to VRAM
macro LoadGraphicsDataIntoVRAM
    ld de, GRAPHICS_DATA_ADDRESS_START
    ld hl, _VRAM8000
    .load_tile\@
        ld a, [de]
        inc de
        ld [hli], a
        ld a, d
        cp a, high(GRAPHICS_DATA_ADDRESS_END)
        jr nz, .load_tile\@
endm

macro ClearTileMap0
    ld hl, _SCRN0
    .erase_tile\@
    xor a
    ld [hli], a
    ld a, h
    cp a, high(_SCRN1)
    jr nz, .erase_tile\@
endm

macro DrawKeyboard
    ld de, FONT_0_OFFSET
    ld hl, _SCRN0 + 32 + 1
    ld c, 10
    .load_tile_row_1\@
        copy [hli], low(de)
        inc de
        dec c
        jr nz, .load_tile_row_1\@
    ; Next row of letters
    ld hl, _SCRN0 + 64 + 1
    ld de, FONT_A_OFFSET
    ld c, 10
    .load_tile_row_2\@
        copy [hli], low(de)
        inc de
        dec c
        jr nz, .load_tile_row_2\@
    ld hl, _SCRN0 + 96 + 1
    ld c, 10
    .load_tile_row_3\@
        copy [hli], low(de)
        inc de
        dec c
        jr nz, .load_tile_row_3\@
    ld hl, _SCRN0 + 128 + 1
    ld c, 10
    .load_tile_row_4\@
        copy [hli], low(de)
        inc de
        dec c
        jr nz, .load_tile_row_4\@
endm

macro DrawEditPlayerNames
    ld hl, WRAM_PLAYER_1_NAME
    ld d, high(EDIT_PLAYER_NAME_DISPLAY)
    ld e, low(EDIT_PLAYER_NAME_DISPLAY)
    ld c, 8
.loop_1\@
    copy [de], [hli]
    inc de
    dec c
    jr nz, .loop_1\@
    ld d, high(EDIT_PLAYER_NAME_DISPLAY + 32)
    ld e, low(EDIT_PLAYER_NAME_DISPLAY + 32)
    ld c, 8
.loop_2\@
    copy [de], [hli]
    inc de
    dec c
    jr nz, .loop_2\@
    ld d, high(EDIT_PLAYER_NAME_DISPLAY + 64)
    ld e, low(EDIT_PLAYER_NAME_DISPLAY + 64)
    ld c, 8
.loop_3\@
    copy [de], [hli]
    inc de
    dec c
    jr nz, .loop_3\@
    ld d, high(EDIT_PLAYER_NAME_DISPLAY + 96)
    ld e, low(EDIT_PLAYER_NAME_DISPLAY + 96)
    ld c, 8
.loop_4\@
    copy [de], [hli]
    inc de
    dec c
    jr nz, .loop_4\@
endm

macro DrawEditInstructions
    ld hl, EDIT_INSTRUCTIONS_DESTINATION
    ld d, high(EDIT_INSTRUCTIONS_SOURCE)
    ld e, low(EDIT_INSTRUCTIONS_SOURCE)
    ld c, 127
.loop\@
    copy [hli], [de]
    inc de
    dec c
    jp nz, .loop\@
endm

; LoadLetterFromMapIndex 0, CHAR_A_ADDR_OFFSET
; LoadLetterFromMapIndex 1, CHAR_Z_ADDR_OFFSET
macro LoadLetterFromAddressOffset
    ld c, 255
    ld hl, TILE_ALPHABET_START
    ld d, high(\2)
    ld e, low(\2)
    add hl, de
    ld_r16_r16 de, hl
    ld hl, _VRAM8800 + (\1 * 256)
.load_letter_copy\@
    copy [hli], [de]
    inc de
    dec c
    jr nz, .load_letter_copy\@
endm

; LoadLetter 0, N (N = 0 - 63)
macro LoadLetterFromMapIndex
    ld h, 0
    ld l, \2
    sla l
    ld d, high(ALPHABET_MAP_START)
    ld e, low(ALPHABET_MAP_START)
    add hl, de
    copy e, [hl]
    inc hl
    copy d, [hl]
    LoadLetterFromAddressOffset \1, de
endm

macro DrawScore
    ld h, high(\1)
    ld l, low(\1)
    ld a, [\2]
    cp a, 10
    jr nc, .draw_double_digit_score\@
    ld b, a
    ld a, FONT_0_OFFSET
    add a, b
    ld [hl], a
    inc hl
    ld [hl], FONT_SPACE_OFFSET
    jr .draw_score_done\@
.draw_double_digit_score\@
    ld b, 255
.score_count_tens\@
    inc b
    sub a, 10
    jr nc, .score_count_tens\@
    ; b contains tens digit
    add a, 10
    ld c, a
    ; c contains ones digit
    ld a, FONT_0_OFFSET
    add a, b
    ld [hl], a
    inc hl
    ld a, FONT_0_OFFSET
    add a, c
    ld [hl], a
.draw_score_done\@
endm

macro HideKeyHighlights
    ld hl, _OAMRAM + sizeof_OAM_ATTRS * 32
    copy [hl], 0
    ld hl, _OAMRAM + sizeof_OAM_ATTRS * 33
    copy [hl], 0
endm

macro DrawScores
    DrawScore PLAYER_1_SCORE_ADDRESS, WRAM_PLAYER_1_SCORE
    DrawScore PLAYER_2_SCORE_ADDRESS, WRAM_PLAYER_2_SCORE
    DrawScore PLAYER_3_SCORE_ADDRESS, WRAM_PLAYER_3_SCORE
    DrawScore PLAYER_4_SCORE_ADDRESS, WRAM_PLAYER_4_SCORE
endm

macro SetDefaultPlayerNames
    copy [WRAM_PLAYER_1_NAME + 0], FONT_A_OFFSET + CHAR_P
    copy [WRAM_PLAYER_2_NAME + 0], FONT_A_OFFSET + CHAR_P
    copy [WRAM_PLAYER_3_NAME + 0], FONT_A_OFFSET + CHAR_P
    copy [WRAM_PLAYER_4_NAME + 0], FONT_A_OFFSET + CHAR_P
    copy [WRAM_PLAYER_1_NAME + 1], FONT_A_OFFSET + CHAR_L
    copy [WRAM_PLAYER_2_NAME + 1], FONT_A_OFFSET + CHAR_L
    copy [WRAM_PLAYER_3_NAME + 1], FONT_A_OFFSET + CHAR_L
    copy [WRAM_PLAYER_4_NAME + 1], FONT_A_OFFSET + CHAR_L
    copy [WRAM_PLAYER_1_NAME + 2], FONT_A_OFFSET + CHAR_A
    copy [WRAM_PLAYER_2_NAME + 2], FONT_A_OFFSET + CHAR_A
    copy [WRAM_PLAYER_3_NAME + 2], FONT_A_OFFSET + CHAR_A
    copy [WRAM_PLAYER_4_NAME + 2], FONT_A_OFFSET + CHAR_A
    copy [WRAM_PLAYER_1_NAME + 3], FONT_A_OFFSET + CHAR_Y
    copy [WRAM_PLAYER_2_NAME + 3], FONT_A_OFFSET + CHAR_Y
    copy [WRAM_PLAYER_3_NAME + 3], FONT_A_OFFSET + CHAR_Y
    copy [WRAM_PLAYER_4_NAME + 3], FONT_A_OFFSET + CHAR_Y
    copy [WRAM_PLAYER_1_NAME + 4], FONT_A_OFFSET + CHAR_E
    copy [WRAM_PLAYER_2_NAME + 4], FONT_A_OFFSET + CHAR_E
    copy [WRAM_PLAYER_3_NAME + 4], FONT_A_OFFSET + CHAR_E
    copy [WRAM_PLAYER_4_NAME + 4], FONT_A_OFFSET + CHAR_E
    copy [WRAM_PLAYER_1_NAME + 5], FONT_A_OFFSET + CHAR_R
    copy [WRAM_PLAYER_2_NAME + 5], FONT_A_OFFSET + CHAR_R
    copy [WRAM_PLAYER_3_NAME + 5], FONT_A_OFFSET + CHAR_R
    copy [WRAM_PLAYER_4_NAME + 5], FONT_A_OFFSET + CHAR_R
    copy [WRAM_PLAYER_1_NAME + 6], FONT_SPACE_OFFSET
    copy [WRAM_PLAYER_2_NAME + 6], FONT_SPACE_OFFSET
    copy [WRAM_PLAYER_3_NAME + 6], FONT_SPACE_OFFSET
    copy [WRAM_PLAYER_4_NAME + 6], FONT_SPACE_OFFSET
    copy [WRAM_PLAYER_1_NAME + 7], FONT_0_OFFSET + 1
    copy [WRAM_PLAYER_2_NAME + 7], FONT_0_OFFSET + 2
    copy [WRAM_PLAYER_3_NAME + 7], FONT_0_OFFSET + 3
    copy [WRAM_PLAYER_4_NAME + 7], FONT_0_OFFSET + 4
endm

macro LoadSave
    ; enable the ERAM
    ld a, $0A
    ld [rRAMG], a

    ; check the header
    ld hl, _SRAM
    ld a, [hli]
    cp a, SAVE_HEADER_D
    jr nz, .done
    ld a, [hli]
    cp a, SAVE_HEADER_M
    jr nz, .done
    ld a, [hli]
    cp a, SAVE_HEADER_G
    jr nz, .done

    copy [WRAM_PLAYER_1_SCORE], [hli]
    copy [WRAM_PLAYER_2_SCORE], [hli]
    copy [WRAM_PLAYER_3_SCORE], [hli]
    copy [WRAM_PLAYER_4_SCORE], [hli]

    ld c, 8 * 4
    ld d, high(WRAM_PLAYER_1_NAME)
    ld e, low(WRAM_PLAYER_1_NAME)
.player_name_copy_loop
    copy [de], [hli]
    inc de
    dec c
    jp nz, .player_name_copy_loop

    .done
    ; disable the ERAM
    ld a, $00
    ld [rRAMG], a
endm

macro WriteSave
    ; enable the ERAM
    ld a, $0A
    ld [rRAMG], a

    ; write the header
    ld hl, _SRAM
    ld a, SAVE_HEADER_D
    ld [hli], a
    ld a, SAVE_HEADER_M
    ld [hli], a
    ld a, SAVE_HEADER_G
    ld [hli], a
    
    copy [hli], [WRAM_PLAYER_1_SCORE]
    copy [hli], [WRAM_PLAYER_2_SCORE]
    copy [hli], [WRAM_PLAYER_3_SCORE]
    copy [hli], [WRAM_PLAYER_4_SCORE]

    ; Save the player names
    ld c, 8 * 4
    ld d, high(WRAM_PLAYER_1_NAME)
    ld e, low(WRAM_PLAYER_1_NAME)
.player_name_copy_loop
    copy [hli], [de]
    inc de
    dec c
    jp nz, .player_name_copy_loop

    ; disable the ERAM
    ld a, $00
    ld [rRAMG], a
endm

macro TurnOffLCD
    xor a
    ld [rLCDC], a
endm

macro TurnOnLCD
    ld a, LCDCF_ON | LCDCF_WIN9C00 | LCDCF_BG8000 | LCDCF_BG9800 | LCDCF_OBJ8 | LCDCF_OBJON | LCDCF_BGON | LCDCF_WINON
    ld [rLCDC], a
endm

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

section "vblank_interrupt", rom0[$0040]
    push af
    copy [WRAM_IS_VBLANK], 1
    pop af
    reti

section "joypad_interrupt", rom0[$0060]
    push af
    copy [WRAM_RANDOM_SALT], [rDIV]
    pop af
    reti

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

section "SmartMouth", rom0

InitSample:
    ; Set WRAM variables
    InitPadInput WRAM_PAD_INPUT
    copy [WRAM_MODE], MODE_GAMEPLAY
    copy [WRAM_TILE_0_X], 36
    copy [WRAM_TILE_0_Y], 36
    copy [WRAM_TILE_1_X], 36 + 8 * 9
    copy [WRAM_TILE_1_Y], 36
    copy [WRAM_RANDOM_SALT], [rDIV]
    copy [WRAM_TILE_0_CHAR], CHAR_A
    copy [WRAM_TILE_1_CHAR], CHAR_A
    copy [WRAM_TILE_TO_UPDATE], 0
    copy [WRAM_TILE_INDEX], 1
    copy [WRAM_PLAYER_ARROW_INDEX], 0
    copy [WRAM_PLAYER_1_SCORE], 0
    copy [WRAM_PLAYER_2_SCORE], 0
    copy [WRAM_PLAYER_3_SCORE], 0
    copy [WRAM_PLAYER_4_SCORE], 0
    copy [WRAM_MENU_ARROW_INDEX], 0
    copy [WRAM_GAME_INTERFACE_NEEDS_INIT], 0
    copy [WRAM_EDIT_INTERFACE_NEEDS_INIT], 1
    copy [WRAM_KEY_HIGHLIGHT_X], 16
    copy [WRAM_KEY_HIGHLIGHT_Y], 24
    copy [WRAM_EDIT_NAME_PLAYER_INDEX], 0
    copy [WRAM_EDIT_NAME_CHAR_INDEX], 8
    SetDefaultPlayerNames


    ; Load the save file
    LoadSave

    ; init the palette
    ld a, %11100100
    ld [rBGP], a
    ld [rOBP0], a
    ld a, %00011110
    ld [rOBP1], a

    ; Set screen scroll
    xor a
    ld [rSCX], a
    ld [rSCY], a
    copy [rWX], WINDOW_X
    copy [rWY], WINDOW_HIDDEN_Y

    ; Init Graphics
    call InitOAM
    LoadGraphicsDataIntoVRAM
    ; LoadLetterFromAddressOffset 0, CHAR_A_ADDR_OFFSET
    ; LoadLetterFromAddressOffset 1, CHAR_Z_ADDR_OFFSET

    call DrawGameTileLetters
    call DrawPlayerNames

    ; set the graphics parameters and turn back LCD on
    TurnOnLCD

    ; enable interrupts
    ld a, IEF_VBLANK | IEF_HILO
    ld [rIE], a
    ei

    ret

UpdateSample:
    ld hl, WRAM_IS_VBLANK
    xor a
    .wait_vblank
        halt
        cp a, [hl]
        jr z, .wait_vblank
        ld [hl], a

    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ; vblank
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    UpdatePadInput WRAM_PAD_INPUT

    ld a, [WRAM_MODE]
    cp a, MODE_GAMEPLAY
    jr nz, .game_mode_done
    call GameplayLogic
.game_mode_done
    cp a, MODE_MENU
    jr nz, .menu_mode_done
    call MenuMode
.menu_mode_done
    cp a, MODE_EDIT_NAMES
    jr nz, .edit_names_mode_done
    call EditNamesMode
.edit_names_mode_done

    ; Mark end of VBlank
    copy [WRAM_IS_VBLANK], 0
    ret

export InitSample, UpdateSample

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

InitOAM:
    ; clear the OAM
    ld c, OAM_COUNT
    ld hl, _OAMRAM + OAMA_Y
    ld de, sizeof_OAM_ATTRS
    .init_oam
        ld [hl], 0
        add hl, de
        dec c
        jr nz, .init_oam
    ret

DrawGameTileLetters:
    ; Draw the first Game Tile Letter
    ld b, 4
    copy e, [WRAM_TILE_0_Y]
    copy [WRAM_TILE_INDEX], $80
    ld hl, _OAMRAM
.DrawTile0Line
    ld c, 4
    copy d, [WRAM_TILE_0_X]
.DrawTile0Chr
    ; Set Y position
    copy [hli], e
    ; Set X position
    copy [hli], d
    ; Increment X position
    ld a, d
    add a, 8
    ld d, a
    ; Set tile index
    copy [hli], [WRAM_TILE_INDEX]
    ; Increment tile index
    ld a, [WRAM_TILE_INDEX]
    inc a
    ld [WRAM_TILE_INDEX], a
    ; Set palette
    copy [hli], OAMF_PAL1
    dec c
    jr nz, .DrawTile0Chr
    ; Increment Y position
    ld a, e
    add a, 8
    ld e, a
    dec b
    jr nz, .DrawTile0Line

    ; Draw the second Game Tile Letter
    ld b, 4
    copy e, [WRAM_TILE_1_Y]
    copy [WRAM_TILE_INDEX], $90

.DrawTile1Line
    ld c, 4
    copy d, [WRAM_TILE_1_X]
.DrawTile1Chr
    ; Set Y position
    copy [hli], e
    ; Set X position
    copy [hli], d
    ; Increment X position
    ld a, d
    add a, 8
    ld d, a
    ; Set tile index
    copy [hli], [WRAM_TILE_INDEX]
    ; Increment tile index
    ld a, [WRAM_TILE_INDEX]
    inc a
    ld [WRAM_TILE_INDEX], a
    ; Set palette
    copy [hli], OAMF_PAL1
    dec c
    jr nz, .DrawTile1Chr
    ; Increment Y position
    ld a, e
    add a, 8
    ld e, a
    dec b
    jr nz, .DrawTile1Line
    ret

DrawPlayerNames:
    ld d, high(WRAM_PLAYER_1_NAME)
    ld e, low(WRAM_PLAYER_1_NAME)
    ld hl, INITIAL_PLAYER_ARROW_ADDRESS + 2
    ld c, 8
.loop_1
    copy [hli], [de]
    inc de
    dec c
    jr nz, .loop_1

    ld d, high(WRAM_PLAYER_2_NAME)
    ld e, low(WRAM_PLAYER_2_NAME)
    ld hl, INITIAL_PLAYER_ARROW_ADDRESS + ARROW_ADDRESS_STEP + 2
    ld c, 8
.loop_2
    copy [hli], [de]
    inc de
    dec c
    jr nz, .loop_2

    ld d, high(WRAM_PLAYER_3_NAME)
    ld e, low(WRAM_PLAYER_3_NAME)
    ld hl, INITIAL_PLAYER_ARROW_ADDRESS + ARROW_ADDRESS_STEP * 2 + 2
    ld c, 8
.loop_3
    copy [hli], [de]
    inc de
    dec c
    jr nz, .loop_3

    ld d, high(WRAM_PLAYER_4_NAME)
    ld e, low(WRAM_PLAYER_4_NAME)
    ld hl, INITIAL_PLAYER_ARROW_ADDRESS + ARROW_ADDRESS_STEP * 3 + 2
    ld c, 8
.loop_4
    copy [hli], [de]
    inc de
    dec c
    jr nz, .loop_4
    ret

DrawActiveArrow:
    ld a, [de]
    ld b, 0
    ld d, 0
    ld e, ARROW_ADDRESS_STEP
    ld [hl], FONT_SPACE_OFFSET
    cp a, b
    jr nz, .player_1_arrow_done
    ld [hl], FONT_FILLED_ARROW_OFFSET
.player_1_arrow_done
    add hl, de
    inc b
    ld [hl], FONT_SPACE_OFFSET
    cp a, b
    jr nz, .player_2_arrow_done
    ld [hl], FONT_FILLED_ARROW_OFFSET
.player_2_arrow_done
    add hl, de
    inc b
    ld [hl], FONT_SPACE_OFFSET
    cp a, b
    jr nz, .player_3_arrow_done
    ld [hl], FONT_FILLED_ARROW_OFFSET
.player_3_arrow_done
    add hl, de
    inc b
    ld [hl], FONT_SPACE_OFFSET
    cp a, b
    jr nz, .player_4_arrow_done
    ld [hl], FONT_FILLED_ARROW_OFFSET
.player_4_arrow_done
    ret

DrawKeyHighlight:
    ld hl, _OAMRAM + sizeof_OAM_ATTRS * 32
    ; Draw the Keyboard highlight
    copy [hli], [WRAM_KEY_HIGHLIGHT_Y]
    copy [hli], [WRAM_KEY_HIGHLIGHT_X]
    copy [hli], $02 ; Filled tile
    copy [hli], OAMF_PRI | OAMF_PAL0
    ; Draw the edit name active character highlight
    ld d, EDIT_PLAYER_NAME_ROW_PX
    ; Load the current player index we're editing
    ld a, [WRAM_EDIT_NAME_PLAYER_INDEX]
    ; Multiply by 8 to get pixel offset
    sla a
    sla a
    sla a
    ; Add to initial pixel value
    add a, d
    copy [hli], a
    ld a, [WRAM_EDIT_NAME_CHAR_INDEX]
    ; Convert from tiles to pixels
    sla a
    sla a
    sla a
    add a, 16
    ld [hli], a
    copy [hli], $02 ; Filled tile
    copy [hli], OAMF_PRI | OAMF_PAL0
    ret

DrawEditInterace:
    TurnOffLCD
    call InitOAM
    ClearTileMap0
    DrawKeyboard
    DrawEditPlayerNames
    DrawEditInstructions
    TurnOnLCD
    ret

GameplayLogic:
    ld d, high(WRAM_PLAYER_ARROW_INDEX)
    ld e, low(WRAM_PLAYER_ARROW_INDEX)
    ld h, high(INITIAL_PLAYER_ARROW_ADDRESS)
    ld l, low(INITIAL_PLAYER_ARROW_ADDRESS)
    call DrawActiveArrow
    DrawScores

    ld a, [WRAM_GAME_INTERFACE_NEEDS_INIT]
    cp a, 0
    jr z, .game_interface_init_done
    TurnOffLCD
    LoadGraphicsDataIntoVRAM
    HideKeyHighlights
    call DrawGameTileLetters
    call DrawPlayerNames
    TurnOnLCD
    copy [WRAM_GAME_INTERFACE_NEEDS_INIT], 0
.game_interface_init_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_A
    jr nz, .display_new_letters_done
        ; Turn off LCD - Make sure this is near the top of VBlank
        TurnOffLCD
        ld a, [WRAM_TILE_0_CHAR]
        LoadLetterFromMapIndex 0, a
        ld a, [WRAM_TILE_1_CHAR]
        LoadLetterFromMapIndex 1, a
        TurnOnLCD
    .display_new_letters_done

    ld a, [rDIV]
    ld b, a
    ld a, [WRAM_RANDOM_SALT]
    xor a, b
    and a, %00111111
    ld b, a
    ld a, [WRAM_TILE_TO_UPDATE]
    cp a, 0
    jr z, .update_tile_0_done
    ld a, b
    ld [WRAM_TILE_0_CHAR], a
    jr .update_tile_1_done
.update_tile_0_done
    ld a, b
    ld [WRAM_TILE_1_CHAR], a
.update_tile_1_done
    ld a, [WRAM_TILE_TO_UPDATE]
    cpl a
    ld [WRAM_TILE_TO_UPDATE], a
.random_letter_done


; Perform work that can be done outside of VBlank
    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_DOWN
    jr nz, .increment_arrow_done
    ld a, [WRAM_PLAYER_ARROW_INDEX]
    inc a
    and a, %00000011
    ld [WRAM_PLAYER_ARROW_INDEX], a
.increment_arrow_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_UP
    jr nz, .decrement_arrow_done
    ld a, [WRAM_PLAYER_ARROW_INDEX]
    dec a
    and a, %00000011
    ld [WRAM_PLAYER_ARROW_INDEX], a
.decrement_arrow_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_RIGHT
    jr nz, .increment_score_done
    ld a, [WRAM_PLAYER_ARROW_INDEX]
    ld b, a
    ld d, high(WRAM_PLAYER_1_SCORE)
    ld a, low(WRAM_PLAYER_1_SCORE)
    add a, b
    ld e, a
    ld a, [de]
    cp a, 99
    jr z, .increment_score_done
    inc a
    ld [de], a
.increment_score_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_LEFT
    jr nz, .decrement_score_done
    ld a, [WRAM_PLAYER_ARROW_INDEX]
    ld b, a
    ld d, high(WRAM_PLAYER_1_SCORE)
    ld a, low(WRAM_PLAYER_1_SCORE)
    add a, b
    ld e, a
    ld a, [de]
    cp a, 0
    jr z, .decrement_score_done
    dec a
    ld [de], a
.decrement_score_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_START
    jr nz, .show_menu_done
    copy [rWY], WINDOW_Y
    copy [WRAM_MODE], MODE_MENU
    copy [WRAM_MENU_ARROW_INDEX], MENU_EXIT_MENU
.show_menu_done
    ret

MenuMode:
    ld d, high(WRAM_MENU_ARROW_INDEX)
    ld e, low(WRAM_MENU_ARROW_INDEX)
    ld h, high(INITIAL_MENU_ARROW_ADDRESS)
    ld l, low(INITIAL_MENU_ARROW_ADDRESS)
    call DrawActiveArrow

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_A
    jr nz, .menu_item_selected_done
    ; Menu item selected
    ld a, [WRAM_MENU_ARROW_INDEX]
    cp a, MENU_SAVE_GAME
    jr nz, .save_game_done
    WriteSave
    jp .hide_menu
.save_game_done
    ld a, [WRAM_MENU_ARROW_INDEX]
    cp a, MENU_RESET_SCORES
    jr nz, .reset_scores_done
    copy [WRAM_PLAYER_1_SCORE], 0
    copy [WRAM_PLAYER_2_SCORE], 0
    copy [WRAM_PLAYER_3_SCORE], 0
    copy [WRAM_PLAYER_4_SCORE], 0
    jp .hide_menu
.reset_scores_done
    ld a, [WRAM_MENU_ARROW_INDEX]
    cp a, MENU_EDIT_NAMES
    jr nz, .edit_names_done
    copy [WRAM_EDIT_INTERFACE_NEEDS_INIT], 1
    copy [WRAM_EDIT_NAME_PLAYER_INDEX], [WRAM_PLAYER_ARROW_INDEX]
    ; Can't jump to .hide_menu because that sets the mode back to gameplay
    copy [WRAM_MODE], MODE_EDIT_NAMES
    copy [rWY], WINDOW_HIDDEN_Y
    ret
.edit_names_done
    ld a, [WRAM_MENU_ARROW_INDEX]
    cp a, MENU_EXIT_MENU
    jr z, .hide_menu
.menu_item_selected_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_DOWN
    jr nz, .increment_arrow_done
    ld a, [WRAM_MENU_ARROW_INDEX]
    inc a
    and a, %00000011
    ld [WRAM_MENU_ARROW_INDEX], a
.increment_arrow_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_UP
    jr nz, .decrement_arrow_done
    ld a, [WRAM_MENU_ARROW_INDEX]
    dec a
    and a, %00000011
    ld [WRAM_MENU_ARROW_INDEX], a
.decrement_arrow_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_START
    jr nz, .hide_menu_done
.hide_menu
    copy [rWY], WINDOW_HIDDEN_Y
    copy [WRAM_MODE], MODE_GAMEPLAY
.hide_menu_done
    ret

EditNamesMode:
    ; Check if we need to initialize the interface
    ld a, [WRAM_EDIT_INTERFACE_NEEDS_INIT]
    cp a, 1
    jr nz, .edit_interface_drawing_done
    copy [WRAM_EDIT_NAME_CHAR_INDEX], 8
    copy [WRAM_EDIT_INTERFACE_NEEDS_INIT], 0
    call DrawEditInterace
.edit_interface_drawing_done
    call DrawKeyHighlight

    ; Check key input
    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_A
    jr nz, .letter_selected_done
    copy a, [WRAM_EDIT_NAME_CHAR_INDEX]
    ; If we're past the end of the name, do nothing
    cp a, 8
    jr z, .letter_selected_done
    ; Get the start of the VRAM
    ld hl, _SCRN0
    ; Get the highlight Y index
    ld a, [WRAM_KEY_HIGHLIGHT_Y]
    ; Divide by 8 to get tiles
    srl a
    srl a
    srl a
    ; Account for the sprites being 16 Y pixels offset
    sub a, 1
    ld b, 0
    ld c, 32
.y_loop
    dec a
    jr z, .increment_key_y_done
    add hl, bc
    jr .y_loop
.increment_key_y_done
    ld c, 1
    ; Get the highlight X index
    ld a, [WRAM_KEY_HIGHLIGHT_X]
    ; Divide by 8 to get tiles
    srl a
    srl a
    srl a
.x_loop
    dec a
    jr z, .increment_key_x_done
    add hl, bc
    jr .x_loop
.increment_key_x_done
    ld d, h
    ld e, l
    ; First edit the display name
    ld hl, EDIT_PLAYER_NAME_DISPLAY
    ; Navigate to the correct line
    ld b, 0
    ld c, 32
    ld a, [WRAM_EDIT_NAME_PLAYER_INDEX]
.add_char_row_loop
    cp a, 0
    jr z, .add_char_row_loop_done
    add hl, bc
    dec a
    jr .add_char_row_loop
.add_char_row_loop_done
    ld a, [WRAM_EDIT_NAME_CHAR_INDEX]
    inc a
    ld [WRAM_EDIT_NAME_CHAR_INDEX], a
    dec a
    ld c, a
    add hl, bc
    copy [hl], [de]
    ; Now edit the saved name, start at the first player's name, we'll increment next
    ld hl, WRAM_PLAYER_1_NAME
    ; Add the current editing character index
    add hl, bc
    ; Load which player we're currently pointing to
    ; ld b, b
    ld a, [WRAM_EDIT_NAME_PLAYER_INDEX]
    ; Multiply by 8 to account for name length
    sla a
    sla a
    sla a
    ld c, a
    add hl, bc
    copy [hl], [de]
.letter_selected_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_B
    jr nz, .letter_delete_done
    ld a, [WRAM_EDIT_NAME_CHAR_INDEX]
    ; If we're at the start of the name, do nothing
    cp a, 0
    jr z, .letter_delete_done
    dec a
    ld [WRAM_EDIT_NAME_CHAR_INDEX], a
    ; Delete the last letter in the display name
    ld hl, EDIT_PLAYER_NAME_DISPLAY
    ld b, 0
    ld c, a
    add hl, bc
    ; Navigate to the correct line
    ld c, 32
    ld a, [WRAM_EDIT_NAME_PLAYER_INDEX]
.delete_char_row_loop
    cp a, 0
    jr z, .delete_char_row_loop_done
    add hl, bc
    dec a
    jr .delete_char_row_loop
.delete_char_row_loop_done
    copy [hl], FONT_SPACE_OFFSET
    ; Now delete the last letter in the saved name, start at the first player's name, we'll increment next
    ld hl, WRAM_PLAYER_1_NAME
    ; Add the current editing index
    copy c, [WRAM_EDIT_NAME_CHAR_INDEX]
    add hl, bc
    ; Load which player we're currently pointing to
    ; ld b, b
    ld a, [WRAM_EDIT_NAME_PLAYER_INDEX]
    ; Multiply by 8 to account for name length
    sla a
    sla a
    sla a
    ld c, a
    add hl, bc
    copy [hl], FONT_SPACE_OFFSET
.letter_delete_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_UP
    jr nz, .decrement_key_highlight_y_done
    ld a, [WRAM_KEY_HIGHLIGHT_Y]
    sub a, 8
    cp a, KEYBOARD_HIGHLIGHT_MIN_Y
    jr nc, .wrap_min_y_done
    ld a, KEYBOARD_HIGHLIGHT_MAX_Y
.wrap_min_y_done
    ld [WRAM_KEY_HIGHLIGHT_Y], a
.decrement_key_highlight_y_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_DOWN
    jr nz, .increment_key_highlight_y_done
    ld a, [WRAM_KEY_HIGHLIGHT_Y]
    add a, 8
    cp a, KEYBOARD_HIGHLIGHT_MAX_Y + 8
    jr nz, .wrap_max_y_done
    ld a, KEYBOARD_HIGHLIGHT_MIN_Y
.wrap_max_y_done
    ld [WRAM_KEY_HIGHLIGHT_Y], a
.increment_key_highlight_y_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_LEFT
    jr nz, .decrement_key_highlight_x_done
    ld a, [WRAM_KEY_HIGHLIGHT_X]
    sub a, 8
    cp a, KEYBOARD_HIGHLIGHT_MIN_X
    jr nc, .wrap_min_x_done
    ld a, KEYBOARD_HIGHLIGHT_MAX_X
.wrap_min_x_done
    ld [WRAM_KEY_HIGHLIGHT_X], a
.decrement_key_highlight_x_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_RIGHT
    jr nz, .increment_key_highlight_x_done
    ld a, [WRAM_KEY_HIGHLIGHT_X]
    add a, 8
    cp a, KEYBOARD_HIGHLIGHT_MAX_X + 8
    jr nz, .wrap_max_x_done
    ld a, KEYBOARD_HIGHLIGHT_MIN_X
.wrap_max_x_done
    ld [WRAM_KEY_HIGHLIGHT_X], a
.increment_key_highlight_x_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_SELECT
    jr nz, .increment_edit_name_done
    ld a, [WRAM_EDIT_NAME_PLAYER_INDEX]
    inc a
    and a, %00000011
    ld [WRAM_EDIT_NAME_PLAYER_INDEX], a
    copy [WRAM_EDIT_NAME_CHAR_INDEX], 8
.increment_edit_name_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_START
    jr nz, .exit_edit_mode_done
    copy [WRAM_GAME_INTERFACE_NEEDS_INIT], 1
    copy [WRAM_MODE], MODE_GAMEPLAY
.exit_edit_mode_done
    ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 
; AABBBCCCDDDEEEFFGGGHHHIIJKLLLMMMNNNNOOOPPPQRRRSSSSTTTTUUUVWWXYYZ
; Weighted alphabet to make more common letters appear more frequently
section "game_tile_addresses", romx[ALPHABET_MAP_START], bank[1]
dw CHAR_A_ADDR_OFFSET, CHAR_A_ADDR_OFFSET, CHAR_B_ADDR_OFFSET, CHAR_B_ADDR_OFFSET
dw CHAR_B_ADDR_OFFSET, CHAR_C_ADDR_OFFSET, CHAR_C_ADDR_OFFSET, CHAR_C_ADDR_OFFSET
dw CHAR_D_ADDR_OFFSET, CHAR_D_ADDR_OFFSET, CHAR_D_ADDR_OFFSET, CHAR_E_ADDR_OFFSET
dw CHAR_E_ADDR_OFFSET, CHAR_E_ADDR_OFFSET, CHAR_F_ADDR_OFFSET, CHAR_F_ADDR_OFFSET
dw CHAR_G_ADDR_OFFSET, CHAR_G_ADDR_OFFSET, CHAR_G_ADDR_OFFSET, CHAR_H_ADDR_OFFSET
dw CHAR_H_ADDR_OFFSET, CHAR_H_ADDR_OFFSET, CHAR_I_ADDR_OFFSET, CHAR_I_ADDR_OFFSET
dw CHAR_J_ADDR_OFFSET, CHAR_K_ADDR_OFFSET, CHAR_L_ADDR_OFFSET, CHAR_L_ADDR_OFFSET
dw CHAR_L_ADDR_OFFSET, CHAR_M_ADDR_OFFSET, CHAR_M_ADDR_OFFSET, CHAR_M_ADDR_OFFSET
dw CHAR_N_ADDR_OFFSET, CHAR_N_ADDR_OFFSET, CHAR_N_ADDR_OFFSET, CHAR_N_ADDR_OFFSET
dw CHAR_O_ADDR_OFFSET, CHAR_O_ADDR_OFFSET, CHAR_O_ADDR_OFFSET, CHAR_P_ADDR_OFFSET
dw CHAR_P_ADDR_OFFSET, CHAR_P_ADDR_OFFSET, CHAR_Q_ADDR_OFFSET, CHAR_R_ADDR_OFFSET
dw CHAR_R_ADDR_OFFSET, CHAR_R_ADDR_OFFSET, CHAR_S_ADDR_OFFSET, CHAR_S_ADDR_OFFSET
dw CHAR_S_ADDR_OFFSET, CHAR_S_ADDR_OFFSET, CHAR_T_ADDR_OFFSET, CHAR_T_ADDR_OFFSET
dw CHAR_T_ADDR_OFFSET, CHAR_T_ADDR_OFFSET, CHAR_U_ADDR_OFFSET, CHAR_U_ADDR_OFFSET
dw CHAR_U_ADDR_OFFSET, CHAR_V_ADDR_OFFSET, CHAR_W_ADDR_OFFSET, CHAR_W_ADDR_OFFSET
dw CHAR_X_ADDR_OFFSET, CHAR_Y_ADDR_OFFSET, CHAR_Y_ADDR_OFFSET, CHAR_Z_ADDR_OFFSET

section "game_tile_alphabet_data", romx[TILE_ALPHABET_START], bank[1]
incbin "assets/tile_alphabet/A.chr"
incbin "assets/tile_alphabet/B.chr"
incbin "assets/tile_alphabet/C.chr"
incbin "assets/tile_alphabet/D.chr"
incbin "assets/tile_alphabet/E.chr"
incbin "assets/tile_alphabet/F.chr"
incbin "assets/tile_alphabet/G.chr"
incbin "assets/tile_alphabet/H.chr"
incbin "assets/tile_alphabet/I.chr"
incbin "assets/tile_alphabet/J.chr"
incbin "assets/tile_alphabet/K.chr"
incbin "assets/tile_alphabet/L.chr"
incbin "assets/tile_alphabet/M.chr"
incbin "assets/tile_alphabet/N.chr"
incbin "assets/tile_alphabet/O.chr"
incbin "assets/tile_alphabet/P.chr"
incbin "assets/tile_alphabet/Q.chr"
incbin "assets/tile_alphabet/R.chr"
incbin "assets/tile_alphabet/S.chr"
incbin "assets/tile_alphabet/T.chr"
incbin "assets/tile_alphabet/U.chr"
incbin "assets/tile_alphabet/V.chr"
incbin "assets/tile_alphabet/W.chr"
incbin "assets/tile_alphabet/X.chr"
incbin "assets/tile_alphabet/Y.chr"
incbin "assets/tile_alphabet/Z.chr"

section "graphics_data", romx[GRAPHICS_DATA_ADDRESS_START], bank[1]
EmptyTile:
dw $0000, $0000, $0000, $0000, $0000, $0000, $0000, $0000
GameTile:
incbin "assets/GameTile.chr"
Font:
incbin "assets/Font.chr"


section "tilemaps_data", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 1 * SCRN_VX_B], bank[1]
db $00, $00, $00
db $01, $02, $02, $02, $03
db $00, $00, $00, 00
db $01, $02, $02, $02, $03
section "tilemaps_data_row1", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 2 * SCRN_VX_B], bank[1]
db $00, $00, $00
db $02, $02, $02, $02, $02
db $00, $00, $00, 00
db $02, $02, $02, $02, $02
section "tilemaps_data_row2", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 3 * SCRN_VX_B], bank[1]
db $00, $00, $00
db $02, $02, $02, $02, $02
db $00, $00, $00, 00
db $02, $02, $02, $02, $02
section "tilemaps_data_row3", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 4 * SCRN_VX_B], bank[1]
db $00, $00, $00
db $02, $02, $02, $02, $02
db $00, $00, $00, 00
db $02, $02, $02, $02, $02
section "tilemaps_data_row4", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 5 * SCRN_VX_B], bank[1]
db $00, $00, $00
db $02, $02, $02, $02, $02
db $00, $00, $00, 00
db $02, $02, $02, $02, $02
section "tilemaps_data_row5", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 6 * SCRN_VX_B], bank[1]
db $00, $00, $00
db $02, $02, $02, $02, $02
db $00, $00, $00, 00
db $02, $02, $02, $02, $02
section "tilemaps_data_row6", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 7 * SCRN_VX_B], bank[1]
db $00, $00, $00
db $04, $02, $02, $02, $05
db $00, $00, $00, 00
db $04, $02, $02, $02, $05

section "tilemaps_data_row9", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 10 * SCRN_VX_B], bank[1]
db FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_FILLED_ARROW_OFFSET, FONT_SPACE_OFFSET
db FONT_A_OFFSET + CHAR_P, FONT_A_OFFSET + CHAR_L, FONT_A_OFFSET + CHAR_A, FONT_A_OFFSET + CHAR_Y, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_R, FONT_SPACE_OFFSET, FONT_0_OFFSET + 1
db FONT_COLON_OFFSET, FONT_SPACE_OFFSET, FONT_0_OFFSET
section "tilemaps_data_row11", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 12 * SCRN_VX_B], bank[1]
db FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET
db FONT_A_OFFSET + CHAR_P, FONT_A_OFFSET + CHAR_L, FONT_A_OFFSET + CHAR_A, FONT_A_OFFSET + CHAR_Y, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_R, FONT_SPACE_OFFSET, FONT_0_OFFSET + 2
db FONT_COLON_OFFSET, FONT_SPACE_OFFSET, FONT_0_OFFSET
section "tilemaps_data_row13", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 14 * SCRN_VX_B], bank[1]
db FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET
db FONT_A_OFFSET + CHAR_P, FONT_A_OFFSET + CHAR_L, FONT_A_OFFSET + CHAR_A, FONT_A_OFFSET + CHAR_Y, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_R, FONT_SPACE_OFFSET, FONT_0_OFFSET + 3
db FONT_COLON_OFFSET, FONT_SPACE_OFFSET, FONT_0_OFFSET
section "tilemaps_data_row15", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 16 * SCRN_VX_B], bank[1]
db FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET
db FONT_A_OFFSET + CHAR_P, FONT_A_OFFSET + CHAR_L, FONT_A_OFFSET + CHAR_A, FONT_A_OFFSET + CHAR_Y, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_R, FONT_SPACE_OFFSET, FONT_0_OFFSET + 4
db FONT_COLON_OFFSET, FONT_SPACE_OFFSET, FONT_0_OFFSET


section "tilemaps_1_data_row0", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + TILEMAP_BYTE_SIZE], bank[1]
db FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_FILLED_ARROW_OFFSET, FONT_SPACE_OFFSET
db FONT_A_OFFSET + CHAR_S, FONT_A_OFFSET + CHAR_A, FONT_A_OFFSET + CHAR_V, FONT_A_OFFSET + CHAR_E, FONT_SPACE_OFFSET, FONT_A_OFFSET + CHAR_G, FONT_A_OFFSET + CHAR_A, FONT_A_OFFSET + CHAR_M, FONT_A_OFFSET + CHAR_E
section "tilemaps_1_data_row2", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + TILEMAP_BYTE_SIZE + 2 * SCRN_VX_B], bank[1]
db FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET
db FONT_A_OFFSET + CHAR_R, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_S, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_T, FONT_SPACE_OFFSET, FONT_A_OFFSET + CHAR_S, FONT_A_OFFSET + CHAR_C, FONT_A_OFFSET + CHAR_O, FONT_A_OFFSET + CHAR_R, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_S
section "tilemaps_1_data_row4", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + TILEMAP_BYTE_SIZE + 4 * SCRN_VX_B], bank[1]
db FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET
db FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_D, FONT_A_OFFSET + CHAR_I, FONT_A_OFFSET + CHAR_T, FONT_SPACE_OFFSET, FONT_A_OFFSET + CHAR_N, FONT_A_OFFSET + CHAR_A, FONT_A_OFFSET + CHAR_M, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_S
section "tilemaps_1_data_row6", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + TILEMAP_BYTE_SIZE + 6 * SCRN_VX_B], bank[1]
db FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET
db FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_X, FONT_A_OFFSET + CHAR_I, FONT_A_OFFSET + CHAR_T, FONT_SPACE_OFFSET, FONT_A_OFFSET + CHAR_M, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_N, FONT_A_OFFSET + CHAR_U

section "tilemaps_1_data_row27", romx[EDIT_INSTRUCTIONS_SOURCE], bank[1]
db FONT_SPACE_OFFSET, FONT_A_OFFSET + CHAR_A, FONT_SPACE_OFFSET, FONT_HOLLOW_ARROW_OFFSET, FONT_SPACE_OFFSET, FONT_A_OFFSET + CHAR_T, FONT_A_OFFSET + CHAR_Y, FONT_A_OFFSET + CHAR_P, FONT_A_OFFSET + CHAR_E
section "tilemaps_1_data_row28", romx[EDIT_INSTRUCTIONS_SOURCE + SCRN_VX_B], bank[1]
db FONT_SPACE_OFFSET, FONT_A_OFFSET + CHAR_B, FONT_SPACE_OFFSET, FONT_HOLLOW_ARROW_OFFSET, FONT_SPACE_OFFSET, FONT_A_OFFSET + CHAR_D, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_L, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_T, FONT_A_OFFSET + CHAR_E
section "tilemaps_1_data_row29", romx[EDIT_INSTRUCTIONS_SOURCE + 2 * SCRN_VX_B], bank[1]
db FONT_SPACE_OFFSET, FONT_A_OFFSET + CHAR_S, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_L, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_C, FONT_A_OFFSET + CHAR_T, FONT_SPACE_OFFSET, FONT_HOLLOW_ARROW_OFFSET, FONT_SPACE_OFFSET, FONT_A_OFFSET + CHAR_N, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_X, FONT_A_OFFSET + CHAR_T
section "tilemaps_1_data_row30", romx[EDIT_INSTRUCTIONS_SOURCE + 3 * SCRN_VX_B], bank[1]
db FONT_SPACE_OFFSET, FONT_A_OFFSET + CHAR_S, FONT_A_OFFSET + CHAR_T, FONT_A_OFFSET + CHAR_A, FONT_A_OFFSET + CHAR_R, FONT_A_OFFSET + CHAR_T, FONT_SPACE_OFFSET, FONT_HOLLOW_ARROW_OFFSET, FONT_SPACE_OFFSET, FONT_A_OFFSET + CHAR_D, FONT_A_OFFSET + CHAR_O, FONT_A_OFFSET + CHAR_N, FONT_A_OFFSET + CHAR_E
