include "utils.rgbinc"
include "alphabet.rgbinc"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

def TILES_COUNT                     equ (384)
def BYTES_PER_TILE                  equ (16)
def TILES_BYTE_SIZE                 equ (TILES_COUNT * BYTES_PER_TILE)
def TILEMAP_BYTE_SIZE               equ (1024)
def GRAPHICS_DATA_SIZE              equ (TILES_BYTE_SIZE + TILEMAP_BYTE_SIZE)
def GRAPHICS_DATA_ADDRESS_END       equ ($8000)
def GRAPHICS_DATA_ADDRESS_START     equ (GRAPHICS_DATA_ADDRESS_END - GRAPHICS_DATA_SIZE)
def TILE_ALPHABET_SIZE              equ (16 * 16 * 26)
def TILE_ALPHABET_START             equ (GRAPHICS_DATA_ADDRESS_START - TILE_ALPHABET_SIZE)
def ALPHABET_MAP_SIZE               equ (128)
def ALPHABET_MAP_START              equ (TILE_ALPHABET_START - ALPHABET_MAP_SIZE)

def INITIAL_ARROW_ADDRESS           equ (_SCRN0 + 10 * SCRN_VX_B + 2)
def ARROW_ADDRESS_STEP              equ (SCRN_VX_B * 2)
def SCORE_ADDRESS_STEP              equ (ARROW_ADDRESS_STEP)
def PLAYER_1_SCORE_ADDRESS          equ (INITIAL_ARROW_ADDRESS + 12)
def PLAYER_2_SCORE_ADDRESS          equ (PLAYER_1_SCORE_ADDRESS + SCORE_ADDRESS_STEP)
def PLAYER_3_SCORE_ADDRESS          equ (PLAYER_2_SCORE_ADDRESS + SCORE_ADDRESS_STEP)
def PLAYER_4_SCORE_ADDRESS          equ (PLAYER_3_SCORE_ADDRESS + SCORE_ADDRESS_STEP)

def SAVE_HEADER_D                   equ $44
def SAVE_HEADER_M                   equ $4D
def SAVE_HEADER_G                   equ $47

rsset _RAM

def WRAM_IS_VBLANK                  rb 1
def WRAM_PAD_INPUT                  rb sizeof_PAD_INPUT

def WRAM_TILE_0_X                   rb 1
def WRAM_TILE_0_Y                   rb 1
def WRAM_TILE_1_X                   rb 1
def WRAM_TILE_1_Y                   rb 1
def WRAM_RANDOM_SALT                rb 1
def WRAM_TILE_0_CHAR                rb 1
def WRAM_TILE_1_CHAR                rb 1
def WRAM_TILE_TO_UPDATE             rb 1
def WRAM_TILE_INDEX                 rb 1
def WRAM_PLAYER_ARROW_INDEX         rb 1
def WRAM_PLAYER_1_SCORE             rb 1
def WRAM_PLAYER_2_SCORE             rb 1
def WRAM_PLAYER_3_SCORE             rb 1
def WRAM_PLAYER_4_SCORE             rb 1

def WRAM_END                        rb 0

; sanity checks
def WRAM_USAGE                      equ (WRAM_END - _RAM)
println "WRAM usage: {d:WRAM_USAGE} bytes"
assert WRAM_USAGE <= $2000, "Too many bytes used in WRAM"

; load the graphics data from ROM to VRAM
macro LoadGraphicsDataIntoVRAM
    ld de, GRAPHICS_DATA_ADDRESS_START
    ld hl, _VRAM8000
    .load_tile\@
        ld a, [de]
        inc de
        ld [hli], a
        ld a, d
        cp a, high(GRAPHICS_DATA_ADDRESS_END)
        jr nz, .load_tile\@
endm

; LoadLetterFromMapIndex 0, CHAR_A_ADDR_OFFSET
; LoadLetterFromMapIndex 1, CHAR_Z_ADDR_OFFSET
macro LoadLetterFromAddressOffset
    ld c, 255
    ld hl, TILE_ALPHABET_START
    ld d, high(\2)
    ld e, low(\2)
    add hl, de
    ld_r16_r16 de, hl
    ld hl, _VRAM8800 + (\1 * 256)
.load_letter_copy\@
    copy [hli], [de]
    inc de
    dec c
    jr nz, .load_letter_copy\@
endm

; LoadLetter 0, N (N = 0 - 63)
macro LoadLetterFromMapIndex
    ld h, 0
    ld l, \2
    sla l
    ld d, high(ALPHABET_MAP_START)
    ld e, low(ALPHABET_MAP_START)
    add hl, de
    copy e, [hl]
    inc hl
    copy d, [hl]
    LoadLetterFromAddressOffset \1, de
endm

macro DrawActivePlayerArrow
    ld a, [WRAM_PLAYER_ARROW_INDEX]
    ld b, 0
    ld h, high(INITIAL_ARROW_ADDRESS)
    ld l, low(INITIAL_ARROW_ADDRESS)
    ld d, 0
    ld e, ARROW_ADDRESS_STEP
    ld [hl], FONT_SPACE_OFFSET
    cp a, b
    jr nz, .player_1_arrow_done
    ld [hl], FONT_FILLED_ARROW_OFFSET
.player_1_arrow_done
    add hl, de
    inc b
    ld [hl], FONT_SPACE_OFFSET
    cp a, b
    jr nz, .player_2_arrow_done
    ld [hl], FONT_FILLED_ARROW_OFFSET
.player_2_arrow_done
    add hl, de
    inc b
    ld [hl], FONT_SPACE_OFFSET
    cp a, b
    jr nz, .player_3_arrow_done
    ld [hl], FONT_FILLED_ARROW_OFFSET
.player_3_arrow_done
    add hl, de
    inc b
    ld [hl], FONT_SPACE_OFFSET
    cp a, b
    jr nz, .player_4_arrow_done
    ld [hl], FONT_FILLED_ARROW_OFFSET
.player_4_arrow_done
    add hl, de
    inc b
endm

macro DrawScore
    ld h, high(\1)
    ld l, low(\1)
    ld a, [\2]
    cp a, 10
    jr nc, .draw_double_digit_score\@
    ld b, a
    ld a, FONT_0_OFFSET
    add a, b
    ld [hl], a
    inc hl
    ld [hl], FONT_SPACE_OFFSET
    jr .draw_score_done\@
.draw_double_digit_score\@
    ld b, 255
.score_count_tens\@
    inc b
    sub a, 10
    jr nc, .score_count_tens\@
    ; b contains tens digit
    add a, 10
    ld c, a
    ; c contains ones digit
    ld a, FONT_0_OFFSET
    add a, b
    ld [hl], a
    inc hl
    ld a, FONT_0_OFFSET
    add a, c
    ld [hl], a
.draw_score_done\@
endm

macro DrawScores
    DrawScore PLAYER_1_SCORE_ADDRESS, WRAM_PLAYER_1_SCORE
    DrawScore PLAYER_2_SCORE_ADDRESS, WRAM_PLAYER_2_SCORE
    DrawScore PLAYER_3_SCORE_ADDRESS, WRAM_PLAYER_3_SCORE
    DrawScore PLAYER_4_SCORE_ADDRESS, WRAM_PLAYER_4_SCORE
endm

macro LoadSave
    ; enable the ERAM
    ld a, $0A
    ld [rRAMG], a

    ; check the header
    ld hl, _SRAM
    ld a, [hli]
    cp a, SAVE_HEADER_D
    jr nz, .done
    ld a, [hli]
    cp a, SAVE_HEADER_M
    jr nz, .done
    ld a, [hli]
    cp a, SAVE_HEADER_G
    jr nz, .done

    copy [WRAM_PLAYER_1_SCORE], [hli]
    copy [WRAM_PLAYER_2_SCORE], [hli]
    copy [WRAM_PLAYER_3_SCORE], [hli]
    copy [WRAM_PLAYER_4_SCORE], [hl]

    .done
    ; disable the ERAM
    ld a, $00
    ld [rRAMG], a
endm

macro WriteSave
    ; enable the ERAM
    ld a, $0A
    ld [rRAMG], a

    ; write the header
    ld hl, _SRAM
    ld a, SAVE_HEADER_D
    ld [hli], a
    ld a, SAVE_HEADER_M
    ld [hli], a
    ld a, SAVE_HEADER_G
    ld [hli], a
    
    copy [hli], [WRAM_PLAYER_1_SCORE]
    copy [hli], [WRAM_PLAYER_2_SCORE]
    copy [hli], [WRAM_PLAYER_3_SCORE]
    copy [hl], [WRAM_PLAYER_4_SCORE]

    ; disable the ERAM
    ld a, $00
    ld [rRAMG], a
endm


; clear the OAM
macro InitOAM
    ld c, OAM_COUNT
    ld hl, _OAMRAM + OAMA_Y
    ld de, sizeof_OAM_ATTRS
    .init_oam\@
        ld [hl], 0
        add hl, de
        dec c
        jr nz, .init_oam\@
endm

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

section "vblank_interrupt", rom0[$0040]
    push af
    copy [WRAM_IS_VBLANK], 1
    pop af
    reti

section "joypad_interrupt", rom0[$0060]
    push af
    copy [WRAM_RANDOM_SALT], [rDIV]
    pop af
    reti

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

section "sample", rom0

InitSample:
    ; Set WRAM variables
    InitPadInput WRAM_PAD_INPUT
    copy [WRAM_TILE_0_X], 36
    copy [WRAM_TILE_0_Y], 36
    copy [WRAM_TILE_1_X], 36 + 8 * 9
    copy [WRAM_TILE_1_Y], 36
    copy [WRAM_RANDOM_SALT], [rDIV]
    copy [WRAM_TILE_0_CHAR], CHAR_A
    copy [WRAM_TILE_1_CHAR], CHAR_A
    copy [WRAM_TILE_TO_UPDATE], 0
    copy [WRAM_TILE_INDEX], 1
    copy [WRAM_PLAYER_ARROW_INDEX], 0
    copy [WRAM_PLAYER_1_SCORE], 0
    copy [WRAM_PLAYER_2_SCORE], 0
    copy [WRAM_PLAYER_3_SCORE], 0
    copy [WRAM_PLAYER_4_SCORE], 0

    ; Load the save file
    LoadSave

    ; init the palette
    ld a, %11100100
    ld [rBGP], a
    ld [rOBP0], a
    ld a, %00011110
    ld [rOBP1], a

    ; Set screen scroll
    xor a
    ld [rSCX], a
    ld [rSCY], a

    ; Init Graphics
    InitOAM
    LoadGraphicsDataIntoVRAM
    LoadLetterFromAddressOffset 0, CHAR_A_ADDR_OFFSET
    LoadLetterFromAddressOffset 1, CHAR_Z_ADDR_OFFSET

    ; Draw the first Game Tile
    ld b, 4
    copy e, [WRAM_TILE_0_Y]
    copy [WRAM_TILE_INDEX], $80
    ld hl, _OAMRAM
.DrawTile0Line
    ld c, 4
    copy d, [WRAM_TILE_0_X]
.DrawTile0Chr
    ; Set Y position
    copy [hli], e
    ; Set X position
    copy [hli], d
    ; Increment X position
    ld a, d
    add a, 8
    ld d, a
    ; Set tile index
    copy [hli], [WRAM_TILE_INDEX]
    ; Increment tile index
    ld a, [WRAM_TILE_INDEX]
    inc a
    ld [WRAM_TILE_INDEX], a
    ; Set palette
    copy [hli], OAMF_PAL1
    dec c
    jr nz, .DrawTile0Chr
    ; Increment Y position
    ld a, e
    add a, 8
    ld e, a
    dec b
    jr nz, .DrawTile0Line

    ; Draw the second Game Tile
    ld b, 4
    copy e, [WRAM_TILE_1_Y]
    copy [WRAM_TILE_INDEX], $90

.DrawTile1Line
    ld c, 4
    copy d, [WRAM_TILE_1_X]
.DrawTile1Chr
    ; Set Y position
    copy [hli], e
    ; Set X position
    copy [hli], d
    ; Increment X position
    ld a, d
    add a, 8
    ld d, a
    ; Set tile index
    copy [hli], [WRAM_TILE_INDEX]
    ; Increment tile index
    ld a, [WRAM_TILE_INDEX]
    inc a
    ld [WRAM_TILE_INDEX], a
    ; Set palette
    copy [hli], OAMF_PAL1
    dec c
    jr nz, .DrawTile1Chr
    ; Increment Y position
    ld a, e
    add a, 8
    ld e, a
    dec b
    jr nz, .DrawTile1Line

    ; set the graphics parameters and turn back LCD on
    ld a, LCDCF_ON | LCDCF_WIN9C00 | LCDCF_BG8000 | LCDCF_BG9800 | LCDCF_OBJ8 | LCDCF_OBJON | LCDCF_BGON
    ld [rLCDC], a

    ; enable interrupts
    ld a, IEF_VBLANK | IEF_HILO
    ld [rIE], a
    ei

    ret

UpdateSample:
    ld hl, WRAM_IS_VBLANK
    xor a
    .wait_vblank
        halt
        cp a, [hl]
        jr z, .wait_vblank
        ld [hl], a

    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ; vblank
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    UpdatePadInput WRAM_PAD_INPUT

    DrawActivePlayerArrow
    DrawScores

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_A
    jr nz, .display_new_letters_done
        xor a
        ; Turn off LCD - Make sure this is near the top of VBlank
        ld [rLCDC], a
        ld a, [WRAM_TILE_0_CHAR]
        LoadLetterFromMapIndex 0, a
        ld a, [WRAM_TILE_1_CHAR]
        LoadLetterFromMapIndex 1, a
        ld a, LCDCF_ON | LCDCF_WIN9C00 | LCDCF_BG8000 | LCDCF_BG9800 | LCDCF_OBJ8 | LCDCF_OBJON | LCDCF_BGON
        ld [rLCDC], a
    .display_new_letters_done

    ld a, [rDIV]
    ld b, a
    ld a, [WRAM_RANDOM_SALT]
    xor a, b
    and a, %00111111
    ld b, a
    ld a, [WRAM_TILE_TO_UPDATE]
    cp a, 0
    jr z, .update_tile_0_done
    ld a, b
    ld [WRAM_TILE_0_CHAR], a
    jr .update_tile_1_done
.update_tile_0_done
    ld a, b
    ld [WRAM_TILE_1_CHAR], a
.update_tile_1_done
    ld a, [WRAM_TILE_TO_UPDATE]
    cpl a
    ld [WRAM_TILE_TO_UPDATE], a
.random_letter_done


; Perform work that can be done outside of VBlank
    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_DOWN
    jr nz, .increment_arrow_done
    ld a, [WRAM_PLAYER_ARROW_INDEX]
    inc a
    and a, %00000011
    ld [WRAM_PLAYER_ARROW_INDEX], a
.increment_arrow_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_UP
    jr nz, .decrement_arrow_done
    ld a, [WRAM_PLAYER_ARROW_INDEX]
    dec a
    and a, %00000011
    ld [WRAM_PLAYER_ARROW_INDEX], a
.decrement_arrow_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_RIGHT
    jr nz, .increment_score_done
    ld a, [WRAM_PLAYER_ARROW_INDEX]
    ld b, a
    ld d, high(WRAM_PLAYER_1_SCORE)
    ld a, low(WRAM_PLAYER_1_SCORE)
    add a, b
    ld e, a
    ld a, [de]
    cp a, 99
    jr z, .increment_score_done
    inc a
    ld [de], a
.increment_score_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_LEFT
    jr nz, .decrement_score_done
    ld a, [WRAM_PLAYER_ARROW_INDEX]
    ld b, a
    ld d, high(WRAM_PLAYER_1_SCORE)
    ld a, low(WRAM_PLAYER_1_SCORE)
    add a, b
    ld e, a
    ld a, [de]
    cp a, 0
    jr z, .decrement_score_done
    dec a
    ld [de], a
.decrement_score_done

    TestPadInput_PressedAll WRAM_PAD_INPUT, PADF_START
    jr nz, .save_game_done
    WriteSave
.save_game_done

    ; Mark end of VBlank
    copy [WRAM_IS_VBLANK], 0
    ret

export InitSample, UpdateSample

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; AABBBCCCDDDEEEFFGGGHHHIIJKLLLMMMNNNNOOOPPPQRRRSSSSTTTTUUUVWWXYYZ
; Weighted alphabet to make more common letters appear more frequently
section "game_tile_addresses", romx[ALPHABET_MAP_START], bank[1]
dw CHAR_A_ADDR_OFFSET, CHAR_A_ADDR_OFFSET, CHAR_B_ADDR_OFFSET, CHAR_B_ADDR_OFFSET
dw CHAR_B_ADDR_OFFSET, CHAR_C_ADDR_OFFSET, CHAR_C_ADDR_OFFSET, CHAR_C_ADDR_OFFSET
dw CHAR_D_ADDR_OFFSET, CHAR_D_ADDR_OFFSET, CHAR_D_ADDR_OFFSET, CHAR_E_ADDR_OFFSET
dw CHAR_E_ADDR_OFFSET, CHAR_E_ADDR_OFFSET, CHAR_F_ADDR_OFFSET, CHAR_F_ADDR_OFFSET
dw CHAR_G_ADDR_OFFSET, CHAR_G_ADDR_OFFSET, CHAR_G_ADDR_OFFSET, CHAR_H_ADDR_OFFSET
dw CHAR_H_ADDR_OFFSET, CHAR_H_ADDR_OFFSET, CHAR_I_ADDR_OFFSET, CHAR_I_ADDR_OFFSET
dw CHAR_J_ADDR_OFFSET, CHAR_K_ADDR_OFFSET, CHAR_L_ADDR_OFFSET, CHAR_L_ADDR_OFFSET
dw CHAR_L_ADDR_OFFSET, CHAR_M_ADDR_OFFSET, CHAR_M_ADDR_OFFSET, CHAR_M_ADDR_OFFSET
dw CHAR_N_ADDR_OFFSET, CHAR_N_ADDR_OFFSET, CHAR_N_ADDR_OFFSET, CHAR_N_ADDR_OFFSET
dw CHAR_O_ADDR_OFFSET, CHAR_O_ADDR_OFFSET, CHAR_O_ADDR_OFFSET, CHAR_P_ADDR_OFFSET
dw CHAR_P_ADDR_OFFSET, CHAR_P_ADDR_OFFSET, CHAR_Q_ADDR_OFFSET, CHAR_R_ADDR_OFFSET
dw CHAR_R_ADDR_OFFSET, CHAR_R_ADDR_OFFSET, CHAR_S_ADDR_OFFSET, CHAR_S_ADDR_OFFSET
dw CHAR_S_ADDR_OFFSET, CHAR_S_ADDR_OFFSET, CHAR_T_ADDR_OFFSET, CHAR_T_ADDR_OFFSET
dw CHAR_T_ADDR_OFFSET, CHAR_T_ADDR_OFFSET, CHAR_U_ADDR_OFFSET, CHAR_U_ADDR_OFFSET
dw CHAR_U_ADDR_OFFSET, CHAR_V_ADDR_OFFSET, CHAR_W_ADDR_OFFSET, CHAR_W_ADDR_OFFSET
dw CHAR_X_ADDR_OFFSET, CHAR_Y_ADDR_OFFSET, CHAR_Y_ADDR_OFFSET, CHAR_Z_ADDR_OFFSET

section "game_tile_alphabet_data", romx[TILE_ALPHABET_START], bank[1]
incbin "assets/tile_alphabet/A.chr"
incbin "assets/tile_alphabet/B.chr"
incbin "assets/tile_alphabet/C.chr"
incbin "assets/tile_alphabet/D.chr"
incbin "assets/tile_alphabet/E.chr"
incbin "assets/tile_alphabet/F.chr"
incbin "assets/tile_alphabet/G.chr"
incbin "assets/tile_alphabet/H.chr"
incbin "assets/tile_alphabet/I.chr"
incbin "assets/tile_alphabet/J.chr"
incbin "assets/tile_alphabet/K.chr"
incbin "assets/tile_alphabet/L.chr"
incbin "assets/tile_alphabet/M.chr"
incbin "assets/tile_alphabet/N.chr"
incbin "assets/tile_alphabet/O.chr"
incbin "assets/tile_alphabet/P.chr"
incbin "assets/tile_alphabet/Q.chr"
incbin "assets/tile_alphabet/R.chr"
incbin "assets/tile_alphabet/S.chr"
incbin "assets/tile_alphabet/T.chr"
incbin "assets/tile_alphabet/U.chr"
incbin "assets/tile_alphabet/V.chr"
incbin "assets/tile_alphabet/W.chr"
incbin "assets/tile_alphabet/X.chr"
incbin "assets/tile_alphabet/Y.chr"
incbin "assets/tile_alphabet/Z.chr"

section "graphics_data", romx[GRAPHICS_DATA_ADDRESS_START], bank[1]
EmptyTile:
dw $0000, $0000, $0000, $0000, $0000, $0000, $0000, $0000
GameTile:
incbin "assets/GameTile.chr"
Font:
incbin "assets/Font.chr"


section "tilemaps_data", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 1 * SCRN_VX_B], bank[1]
db $00, $00, $00
db $01, $02, $02, $02, $03
db $00, $00, $00, 00
db $01, $02, $02, $02, $03
section "tilemaps_data_row1", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 2 * SCRN_VX_B], bank[1]
db $00, $00, $00
db $02, $02, $02, $02, $02
db $00, $00, $00, 00
db $02, $02, $02, $02, $02
section "tilemaps_data_row2", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 3 * SCRN_VX_B], bank[1]
db $00, $00, $00
db $02, $02, $02, $02, $02
db $00, $00, $00, 00
db $02, $02, $02, $02, $02
section "tilemaps_data_row3", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 4 * SCRN_VX_B], bank[1]
db $00, $00, $00
db $02, $02, $02, $02, $02
db $00, $00, $00, 00
db $02, $02, $02, $02, $02
section "tilemaps_data_row4", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 5 * SCRN_VX_B], bank[1]
db $00, $00, $00
db $02, $02, $02, $02, $02
db $00, $00, $00, 00
db $02, $02, $02, $02, $02
section "tilemaps_data_row5", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 6 * SCRN_VX_B], bank[1]
db $00, $00, $00
db $02, $02, $02, $02, $02
db $00, $00, $00, 00
db $02, $02, $02, $02, $02
section "tilemaps_data_row6", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 7 * SCRN_VX_B], bank[1]
db $00, $00, $00
db $04, $02, $02, $02, $05
db $00, $00, $00, 00
db $04, $02, $02, $02, $05

section "tilemaps_data_row9", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 10 * SCRN_VX_B], bank[1]
db FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_FILLED_ARROW_OFFSET, FONT_SPACE_OFFSET
db FONT_A_OFFSET + CHAR_P, FONT_A_OFFSET + CHAR_L, FONT_A_OFFSET + CHAR_A, FONT_A_OFFSET + CHAR_Y, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_R, FONT_SPACE_OFFSET, FONT_0_OFFSET + 1
db FONT_COLON_OFFSET, FONT_SPACE_OFFSET, FONT_0_OFFSET
section "tilemaps_data_row11", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 12 * SCRN_VX_B], bank[1]
db FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET
db FONT_A_OFFSET + CHAR_P, FONT_A_OFFSET + CHAR_L, FONT_A_OFFSET + CHAR_A, FONT_A_OFFSET + CHAR_Y, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_R, FONT_SPACE_OFFSET, FONT_0_OFFSET + 2
db FONT_COLON_OFFSET, FONT_SPACE_OFFSET, FONT_0_OFFSET
section "tilemaps_data_row13", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 14 * SCRN_VX_B], bank[1]
db FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET
db FONT_A_OFFSET + CHAR_P, FONT_A_OFFSET + CHAR_L, FONT_A_OFFSET + CHAR_A, FONT_A_OFFSET + CHAR_Y, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_R, FONT_SPACE_OFFSET, FONT_0_OFFSET + 3
db FONT_COLON_OFFSET, FONT_SPACE_OFFSET, FONT_0_OFFSET
section "tilemaps_data_row15", romx[GRAPHICS_DATA_ADDRESS_START + TILES_BYTE_SIZE + 16 * SCRN_VX_B], bank[1]
db FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET, FONT_SPACE_OFFSET
db FONT_A_OFFSET + CHAR_P, FONT_A_OFFSET + CHAR_L, FONT_A_OFFSET + CHAR_A, FONT_A_OFFSET + CHAR_Y, FONT_A_OFFSET + CHAR_E, FONT_A_OFFSET + CHAR_R, FONT_SPACE_OFFSET, FONT_0_OFFSET + 4
db FONT_COLON_OFFSET, FONT_SPACE_OFFSET, FONT_0_OFFSET
